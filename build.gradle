buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			// Needed for FG
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			// Needed for gradle API
			name = "gradle"
			url = "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		// ForgeGradle itself
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

subprojects {
	if (project.name != 'share') {
		task regenEclipse {
			// Delete the eclipse classpath and project and then recreate it, without doing any of the slower bits.
			dependsOn 'cleanEclipseProject'
			dependsOn 'cleanEclipseClasspath'
			dependsOn 'eclipseProject'
			dependsOn 'eclipseClasspath'
		}

		// Compiling
		apply plugin: 'java'
		// idea IDE
		apply plugin: 'idea'
		// eclipse IDE
		apply plugin: 'eclipse'
		// Needed to use content from maven repos
		apply plugin: 'maven'

		group = 'pokechu22.test.begradle'
		version = '0.2.0-SNAPSHOT'
		archivesBaseName = 'begradle'
		targetCompatibility = '1.8'
		sourceCompatibility = '1.8'

		repositories {
			// Main maven repos
			jcenter()
			mavenCentral()
			// Local repository (eg, own build of ForgeGradle)
			// Below those repos so that source jars work (but above FG)
			mavenLocal()
			// Needed for ForgeGradle (among other things) 
			maven {
				name = "forge"
				url = "http://files.minecraftforge.net/maven"
			}
		}

		configurations {
			shade
			compileOnly.extendsFrom shade
		}

		sourceSets {
			main.compileClasspath += configurations.shade;
			main.runtimeClasspath += configurations.shade;
			test.compileClasspath += configurations.shade;
			test.runtimeClasspath += configurations.shade;
		}

		sourceSets {
			main {
				java {
					srcDir "src/main/java"
					srcDir project(':share').file("src/main/java")
				}
				resources {
					srcDir "src/main/resources"
					srcDir project(':share').file("src/main/resources")
				}
			}
			test {
				java {
					srcDir project(':share').file("src/test/java")
				}
			}
		}

		compileJava {
			options.deprecation = true
		}

		jar {

			configurations.shade.each { dep ->
				from(project.zipTree(dep)){
					exclude 'META-INF', 'META-INF/**'
				}
			}

			manifest {
				attributes 'version': project.version
				attributes 'javaCompliance': project.targetCompatibility
				attributes 'group': project.group
				attributes 'Implementation-Version': project.version + getGitHash()
			}
		}

		javadoc {
			classpath += configurations.compileOnly

			// Linked javadoc urls
			options.addStringOption 'link', 'https://gradle.org/docs/current/javadoc/'
		}

		task javadocJar(type: Jar, dependsOn: javadoc) {
			from javadoc
			classifier = "javadoc"
		}

		artifacts {
			archives jar
			archives javadocJar
		}
	}
}

def getGitHash() {
	def process = 'git rev-parse --short HEAD'.execute()
	process.waitFor()
	return '-' + (process.exitValue() > 0 ? 'unknown' : process.text.trim())
}
